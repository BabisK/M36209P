% Αρχείο: flight_requests.pl.
% Παράδειγμα γραμματικής DCG. 
% Ίων Ανδρουτσόπουλος, 26/1/2004.

% Παραδείγματα εκφράσεων που επιτρέπει η γραμματική:
% phrase(s, [θέλω, μια, πτήση, προς, την, αθήνα]).
% phrase(s, [εγώ, θέλω, μια, πτήση, από, την, αθήνα, προς, τη, θεσσαλονίκη]).
% phrase(s, [ένας, πελάτης, επιθυμεί, μια, πτήση, προς, το, ηράκλειο]).
% phrase(s, [ένας, πελάτης, θέλει, μια, πτήση, προς, κάθε, πόλη]).
%
% Θα μπορούσαμε να έχουμε προσθέσει ιδιότητες που να εξασφαλίζουν συμφωνία
% γένους, αριθμού κλπ., όπως στις γραμματικές "gender_agreement.pl" και 
% "gender_case_agreement.pl". 
% Θα μπορούσαμε, επίσης, να επεκτείνουμε τη γραμματική, ώστε να επιστρέφεται
% και το συντακτικό δένδρο, όπως στη γραμματική "tree_structure.pl".

%%%%%% Λεξικό:

% Κύρια ονόματα. Τα γράφουμε με μικρό γιατί διαφορετικά η Prolog τα
% θεωρεί μεταβλητές.
pn --> [αθήνα].
pn --> [θεσσαλονίκη].
pn --> [ηράκλειο].

% Αντωνυμίες:
pron --> [εγώ].

% Ουσιαστικά:
n --> [πτήση].
n --> [πελάτης].
n --> [πελάτη].
n --> [πόλη].
n --> [αεροδρόμιο].

% Ρήματα:
v --> [θέλω].
v --> [θέλει].
v --> [προτιμώ].
v --> [προτιμά].
v --> [επιθυμώ].
v --> [επιθυμεί].

% Προθέσεις:
prep --> [από].
prep --> [προς].

% Άρθρα:
det --> [τη].
det --> [την].
det --> [το].
det --> [τον].
det --> [μια].
det --> [μία].
det --> [ο]. 
det --> [ένας].
det --> [έναν].
det --> [ένα].
det --> [κάθε].

%%%%%% Υπόλοιποι κανόνες της γραμματικής:

% Θεωρούμε ότι η πρόταση αποτελείται από ένα ονομαστικό σύνολο (np) σε ρόλο
% υποκειμένου και ένα ρηματικό σύνολο (vp). 
s --> np, vp.

% Αν δεν υπάρχει υποκείμενο, θα θεωρούμε ότι είναι ο ομιλητής.
s --> vp. 

% Για απλούστευση, θεωρούμε ότι όλα τα ρήματα είναι μεταβατικά, 
% δηλαδή απαιτούν αντικείμενο (το np). 
vp --> v, np.

% Θεωρούμε ότι ένα ονομαστικό σύνολο είναι ένα κύριο όνομα (pn) με
% το άρθρο του ή μια προσωπική αντωνυμία (pron) ή ένα nominal με το άρθρο του.
np --> det, pn.
np --> pron.
np --> det, nominal.

% Ένα nominal θα είναι ένα ουσιαστικό, πιθανώς συνοδευόμενο από 
% εμπρόθετους προσδιορισμούς 
nominal --> n.
nominal --> n, modifiers.

% Ένας ή περισσότεροι εμπρόθετοι προσδιορισμοί (pp):
modifiers --> pp.
modifiers --> pp, modifiers.

% Ένας εμπρόθετος προσδιορισμός (pp) θα είναι μια πρόθεση ακολουθούμενη
% από ένα ονομαστικό σύνολο (π.χ. "από τη Θεσσαλονίκη").
pp --> prep, np.

%%%%%% Τέλος.